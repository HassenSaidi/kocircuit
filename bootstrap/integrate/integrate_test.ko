import "github.com/kocircuit/kocircuit/bootstrap/testutil"
import "github.com/kocircuit/kocircuit/bootstrap/context/place" as ko_place
import "github.com/kocircuit/kocircuit/bootstrap/types"

TestAll() {
	return: TestIntegrate()
}

// ko play github.com/kocircuit/kocircuit/bootstrap/integrate/TestIntegrate
TestIntegrate() {
	return: And(
		testIntegrateBasicToOptional()
		testutil.ShouldPanic(testIntegrateOptionalToBasic)
		testIntegrateBasicToSeries()
		testutil.ShouldPanic(testIntegrateSeriesToBasic)
		testIntegrateValidStructToStruct()
		testutil.ShouldPanic(testIntegrateInvalidStructToStruct)
		testIntegrateValidUnionToUnion()
		testutil.ShouldPanic(testIntegrateInvalidUnionToUnion)
		testIntegratePanic()
	)
}

testIntegrateBasicToOptional() {
	integerType: types.IntegerType()
	optIntegerType: types.OptionalType(integerType)
	return: Equal(
		Integrate(place: ko_place.TestingRoot(), fromType: integerType, toType: optIntegerType)
		(
			intoOptional: (elemShaper: (identity: (), fromType: (integer: ()), toType: (integer: ())))
			fromType: (integer: ())
			toType: (optional: (elemType: (integer: ())))
		)
	)
}

testIntegrateOptionalToBasic() {
	integerType: types.IntegerType()
	optIntegerType: types.OptionalType(integerType)
	return: Integrate(place: ko_place.TestingRoot(), fromType: optIntegerType, toType: integerType)
}

testIntegrateBasicToSeries() {
	integerType: types.IntegerType()
	seriesIntegerType: types.SeriesType(integerType)
	return: Equal(
		Integrate(place: ko_place.TestingRoot(), fromType: integerType, toType: seriesIntegerType)
		(
			intoSeries: (elemShaper: (identity: (), fromType: (integer: ()), toType: (integer: ())))
			fromType: (integer: ())
			toType: (series: (elemType: (integer: ())))
		)
	)
}

testIntegrateSeriesToBasic() {
	integerType: types.IntegerType()
	seriesIntegerType: types.SeriesType(integerType)
	return: Integrate(place: ko_place.TestingRoot(), fromType: seriesIntegerType, toType: integerType)
}

testIntegratePanic() {
	return: Recover(
		invoke: testIntegrateInvalidStructToStruct
		panic: testIntegratePanicRecover(panic?) {
			return: Equal(
				panic.error.fromType
				(series: (elemType: (bool: ())))
			)
		}
	)
}

testIntegrateInvalidStructToStruct() {
	_: Integrate(place: ko_place.TestingRoot(), fromType: testSmallStruct(), toType: testBigStruct())
	return: false
}

testIntegrateValidStructToStruct() {
	return: Equal(
		Integrate(place: ko_place.TestingRoot(), fromType: testBigStruct(), toType: testSmallStruct())
		(
			structureStructure: (
				fieldShapers: (
					(name: "f1", shaper: (identity: (), fromType: (integer: ()), toType: (integer: ())))
					(
						name: "f2"
						shaper: (
							intoSeries: (elemShaper: (identity: (), fromType: (bool: ()), toType: (bool: ())))
							fromType: (bool: ())
							toType: (series: (elemType: (bool: ())))
						)
					)
				)
			)
			fromType: (
				structure: (
					fields: (
						(name: "f1", type: (integer: ()))
						(name: "f2", type: (bool: ()))
						(name: "f3", type: (string: ()))
					)
				)
			)
			toType: (
				structure: (
					fields: (
						(name: "f1", type: (integer: ()))
						(name: "f2", type: (series: (elemType: (bool: ()))))
					)
				)
			)
		)
	)
}

testSmallStruct() {
	return: types.StructureType(
		types.Field(name: "f1", type: types.IntegerType())
		types.Field(name: "f2", type: types.SeriesType(types.BoolType()))
	)
}

testBigStruct() {
	return: types.StructureType(
		types.Field(name: "f1", type: types.IntegerType())
		types.Field(name: "f2", type: types.BoolType())
		types.Field(name: "f3", type: types.StringType())
	)
}

testIntegrateInvalidUnionToUnion() {
	return: Integrate(place: ko_place.TestingRoot(), fromType: testBigUnion(), toType: testSmallUnion())
}

testIntegrateValidUnionToUnion() {
	return: Equal(
		Integrate(place: ko_place.TestingRoot(), fromType: testSmallUnion(), toType: testBigUnion())
		(
			unionUnion: (
				varietyShapers: (
					(
						transform: "M1"
						fieldShapers: (
							(
								name: "a2"
								shaper: (
									intoSeries: (elemShaper: (identity: (), fromType: (bool: ()), toType: (bool: ())))
									fromType: (bool: ())
									toType: (series: (elemType: (bool: ())))
								)
							)
						)
					)
				)
			)
			fromType: (
				union: (varieties: (variety: (transform: "M1", fields: (name: "a2", type: (bool: ())))))
			)
			toType: (
				union: (
					varieties: (
						(
						variety: (transform: "M1", fields: (name: "a2", type: (series: (elemType: (bool: ())))))
						)
						(variety: (transform: "M2"))
					)
				)
			)
		)
	)
}

testBigUnion() {
	return: types.UnionType(
		types.VarietyType(
			transform: "M1"
			fields: types.Field(name: "a2", type: types.SeriesType(types.BoolType()))
		)
		types.VarietyType(transform: "M2")
	)
}

testSmallUnion() {
	return: types.UnionType(
		types.VarietyType(
			transform: "M1"
			fields: types.Field(name: "a2", type: types.BoolType())
		)
	)
}
